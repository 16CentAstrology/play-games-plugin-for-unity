buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.0.1'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        // The following line is needed when building the plugin with an unreleased SDK.
        maven { url '/tmp/games_m2/repository' }
    }
}

apply plugin: 'maven-publish'
apply plugin: 'com.android.library'

group 'com.google.games'
project.ext.baseName = "gpgs-plugin-support"

if (project.hasProperty("projVersion")) {
    project.version = project.projVersion
} else {
    project.version '0-SNAPSHOT'
}

if (project.hasProperty("uploadDir")) {
    project.ext.uploadDir = project.uploadDir
} else {
    project.ext.uploadDir = "${buildDir}/m2repository"
}

android {
    lintOptions {
        abortOnError true
        warningsAsErrors true

        // Don't fail when new versions of dependencies are available. Allows reproducible builds
        disable("NewerVersionAvailable", "GradleDependency")
    }
    defaultConfig {
        versionName  project.version
        archivesBaseName = project.ext.baseName
        minSdkVersion 16

    }
    compileSdkVersion 28
}

// Rename the .aar file to be .srcaar to work around having the local repo in a
// subdirectory in a Unity project.
// The jar resolver plugin handles the changing back from .srcaarto .aar
task renameLibrary(type: Copy) {
    from "${buildDir}/outputs/aar/${baseName}-release.aar"
    into "${buildDir}"
    rename { String fn ->
        fn.replace("release.aar", "${version}.srcaar")
    }
}

tasks.whenTaskAdded { task ->
    if (task.name == 'assembleRelease') {
        renameLibrary.dependsOn(task)
    }
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = project.group
            artifactId = project.ext.baseName
            version = project.version

            artifact("${buildDir}/${baseName}-${version}.srcaar" as File)
        }
    }

    repositories {
        maven {
            url uri(project.ext.uploadDir)
        }
    }
}

tasks.withType(PublishToMavenRepository) { it.dependsOn renameLibrary }

dependencies {
    implementation 'com.google.android.gms:play-services-games-v2:17.0.0'
    implementation 'com.google.android.gms:play-services-nearby:18.5.0'
}
